// Chaiscript Tutorial
// - semicolon at the end of line is optional

// Declaring a vector/list
var vec = [0, 0, 1, 5, 0, 0];

// Function declaration with guards
def process(data) : size(data) > 5 {
    var found = -1;
    for (var i = 0; i < data.size(); ++i) {
        if (data[i] > 1) {
            found = i;
        }
    }

    if (found == -1) {
        throw("Not found exception");
    }

    // return keyword is optional
    return found;
}

// Lambda
var inc = fun(x) { x +1; }
print(inc(1));

// Exception handling
try {
    process(vec);
} catch(e) : is_type(e, "string") {
    print(e);
} catch(e) {
    print("Unknown exception")
}

// Class declaration
class DataContainer {
    var data;

    def DataContainer(data) { this.data = data; }
    def erase() { this.data = "Empty"; }
}

// Class extension (out of class definition)
def DataContainer::add(x) { this.data.push_back(x); }

// Object instantiation
var data = DataContainer(10);
print(data.data);

// In string evaulation
print("${1+5}");

// Dynamic code evaulation
var value = eval("1 + 2 * 3");
print(value);
